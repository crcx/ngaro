#! /usr/bin/toka
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#! This is a Toka script to shrink an image file down in size.
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#! It works by locating the heap pointer, and only writing the
#! actually used portion of the memory back out.
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#! Requires:
#!  - Toka
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#! This code was written by Charles Childers for use with Retro
#! It is gifted to the public domain. In the event that your
#! country does not allow a direct dedication to the public
#! domain, the author voluntarily gives up all rights to the
#! extent permitted by law.
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

include image-lib.toka

value WHERE

[ ( -- )
  ( Find the location of the blocks and store it )
  INPUT 3 cells + @ 1 + to WHERE ] is prepare

[ ( -- )
  ( Close the output file )
  OUTPUT file.close ] is finish

[ ( -- )
  ( Write the blocks to the output file )
  OUTPUT INPUT WHERE cells file.write drop ] is export

( The main sequence )
input: retroImage
output: retroImage
prepare ." Heap ends at: " WHERE . cr export finish bye
