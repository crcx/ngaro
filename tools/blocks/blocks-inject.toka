#! /usr/bin/toka
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#! This is a Toka script to copy the contents of a block file
#! into a Retro image.
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#! Requires:
#!  - Toka
#!  - Extended Retro image file ("retroImage")
#!  - Block file ("blocks"), in the proper format
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#! This code was written by Charles Childers for use with Retro
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

include image-lib.toka

value| IMAGE WHERE |

[ ( "file" -- )
  ( Load "file" as the image the blocks will be merged into )
  wsparse file.slurp keep to IMAGE ] is image:

[ ( -- )
  ( Find the offset for the blocks )
  IMAGE-SIZE cells BLOCKS cells - to WHERE ] is prepare

[ ( -- )
  ( Write the original image, other than blocks, to the output file )
  OUTPUT IMAGE WHERE file.write drop ] is write-image

[ ( -- )
  ( Write the new blocks to the output file )
  OUTPUT INPUT BLOCKS cells file.write drop ] is write-blocks

[ ( -- )
  ( Close the output file )
  OUTPUT file.close ] is finish

( The main sequence )
input: blocks
image: retroImage
output: retroImage
prepare write-image write-blocks finish bye
