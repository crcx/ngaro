#! /usr/bin/toka
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#! This is a Toka script to convert a Retro image into a source
#! file capable of being added to the various VM implementations.
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#! Requires:
#!  - Toka
#!  - Retro image file ("retroImage")
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#! This code was written by Charles Childers for use with Retro
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#! Languages Supported:
#!   C   (console)
#!   C#  (dotnet)
#!   Java (console)
#!   Java (MIDP)
#!   JavaScript
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


value| fid size image |
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ vm
[
  ." #include \"functions.h\"\n"
  ." #include \"vm.h\"\n"
  ." void initial_image(VM *vm)\n{\n"
] is c-console-header
[ ." }\n" ] is c-console-footer
[ ."   vm->image[" i >string type ." ]=" >string type ." ;\n" ] is c-console-line
[
  0 size
  [
    image i cell-size * + @ dup 0 <> [ c-console-line ] [ drop ] ifTrueFalse
  ] countedLoop
] is c-console-process
[ c-console-header c-console-process c-console-footer ] is vm
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ Java and .NET
[ ." memory[" i >string type ." ]=" >string type ." ;\n" ] is java-line
[
  0 size
  [
    image i cell-size * + @ dup 0 <> [ java-line ] [ drop ] ifTrueFalse
  ] countedLoop
] is java-process
[ java-process ] is java
[ java-process ] is dotnet
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ JavaScript
[
  ." function loadImage()\n{\n"
  ."   var a; for (a = 0; a < 32768; a++) image[a]=0;\n"
] is js-header
[ ." }\n" ] is js-footer
[ ." image[" i >string type ." ]=" >string type ." ;\n" ] is js-line
[
  0 size
  [
    image i cell-size * + @ dup 0 <> [ js-line ] [ drop ] ifTrueFalse
  ] countedLoop
] is js-process
[ js-header js-process js-footer ] is javascript
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ midp
[
  ." package Retro;\n"
  ." public class Img {\n"
  ."   private Retroforth root;\n"
  ."   public Img(Retroforth root) {\n"
  ."     this.root = root;\n"
  ."     loadImage();\n"
  ."   }\n\n"
  ."   public void loadImage() {\n"
] is midp-header
[ ." }\n}\n" ] is midp-footer
[ ."   root.image[" i >string type ." ]=" >string type ." ;\n" ] is midp-line
[
  0 size
  [
    image i cell-size * + @ dup 0 <> [ midp-line ] [ drop ] ifTrueFalse
  ] countedLoop
] is midp-process
[ midp-header midp-process midp-footer ] is midp
#! ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ main code follows
( Exit if no arguments were passed                 )
#args 2 > [ ." Try:\n  image2any.toka port imagename\n" bye ] ifFalse

2 arglist array.get "R" file.open to fid
fid file.size cell-size / to size
fid file.close
2 arglist array.get file.slurp keep to image

1 arglist array.get " vm" string.compare [ vm ] ifTrue
1 arglist array.get " java" string.compare [ java ] ifTrue
1 arglist array.get " dotnet" string.compare [ dotnet ] ifTrue
1 arglist array.get " javascript" string.compare [ javascript ] ifTrue
1 arglist array.get " midp" string.compare [ midp ] ifTrue

bye
